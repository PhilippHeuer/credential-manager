plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
    id 'idea'
    id 'eclipse'
    id 'signing'
    id 'io.freefair.lombok' version '5.3.0'
}

group 'com.github.philippheuer.credentialmanager'
description = 'A simple credential manager.'
version = '0.1.3'

sourceCompatibility = 1.8

// Lombok
lombok {
    version = "1.18.24"
}
generateLombokConfig.enabled = false

// Repositories
repositories {
    mavenCentral()
}

// Dependencies
dependencies {
    // SLF4J API
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.11'

    // Commons Lang
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    // HTTP Client
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.10.0'

    // Jackson (JSON)
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.3'

    // Testing
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.philippheuer.credentialmanager'
            artifactId = 'credentialmanager'
            from components.java
            pom {
                name = String.valueOf(project.name).toLowerCase()
                description = project.description
                url = 'https://github.com/PhilippHeuer/credential-manager'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'philippheuer'
                        name = 'Philipp Heuer'
                        email = 'info@philippheuer.me'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/PhilippHeuer/credential-manager.git'
                    developerConnection = 'scm:git:git://github.com/PhilippHeuer/credential-manager.git'
                    url = 'https://github.com/PhilippHeuer/credential-manager'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

nexusPublishing {
    packageGroup = 'com.github.philippheuer.credentialmanager'

    repositories {
        sonatype {
            username = sonatypeUsername
            password = sonatypePassword
        }
    }
}
